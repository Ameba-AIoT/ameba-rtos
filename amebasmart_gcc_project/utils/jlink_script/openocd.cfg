# ameba.cfg
# Usage:
#   openocd  -c "set ADAPTER_TYPE jlink" -c "set ADAPTER_SPEED 2000" -f ameba.cfg
#   openocd  -c "set ADAPTER_TYPE daplink" -c "set ADAPTER_SPEED 4000" -f ameba.cfg

# -----------------------------------------------------------------
# 1. Default Parameters
# -----------------------------------------------------------------
if { [info exists ADAPTER_TYPE] == 0 } {
    set ADAPTER_TYPE "daplink"
}
if { [info exists ADAPTER_SPEED] == 0 } {
    set ADAPTER_SPEED 3000
}

# Optional: allow user to disable cores
if { [info exists ENABLE_CA32] == 0 } {
    set ENABLE_CA32 1
}
if { [info exists ENABLE_KM4] == 0 } {
    set ENABLE_KM4 1
}
if { [info exists ENABLE_KM0] == 0 } {
    set ENABLE_KM0 1
}

if { [info exists CHIPCORES] } {
    set _cores $CHIPCORES
} else {
    set _cores 2
}

if { [info exists GDB_PORT_SPECIFIED] == 1 } {
    echo "GDB port specified: 50000"
} else {
    set GDB_PORT_SPECIFIED 0
}

# Echo config for debugging
echo "Using adapter: $ADAPTER_TYPE"
echo "SWD speed: ${ADAPTER_SPEED} kHz"

# -----------------------------------------------------------------
# 2. Load Adapter Config Dynamically
# -----------------------------------------------------------------
if { $ADAPTER_TYPE eq "jlink" } {
    source [find interface/jlink.cfg]
} elseif { $ADAPTER_TYPE eq "daplink" } {
    source [find interface/cmsis-dap.cfg]
} else {
    error "Unsupported ADAPTER_TYPE: $ADAPTER_TYPE (use 'jlink' or 'daplink')"
}
transport select swd
adapter speed $ADAPTER_SPEED

proc ameba_ca32_debug_init { target_name } {
    echo "=================================================="
    echo "Executing CA32 debug init for target: $target_name"
    echo "=================================================="
    set AP_NUM 3

    # 1. DBG REG BASE ADDR (Segger: _Addr_DBGREGS = _APB_ADDR_DBGREGS_A32_0 = 0x80030000)
    set DBGREGS_BASE 0x80030000

    # 2. REG OFFSET
    set OSLAR_EL1_OFFSET        0x300
    set EDLAR_OFFSET            0xFB0
    set EDPRCR_OFFSET           0x310
    set EDPRSR_OFFSET           0x314
    set EDSCR_OFFSET            0x088
    set EDRCR_OFFSET            0x090

    # 3. CTI REG (Segger defined)
    set _APB_ADDR_CTIREGS_A32_0 0x80038000
    set CTILAR_OFFSET           0xFB0

    ameba.dap apsel $AP_NUM
    set ap_ctrl_val [expr {(2 << 0) | (1 << 4) | (1 << 31)}]
    ameba.dap apreg 3 0 $ap_ctrl_val

    set oslar_addr [expr {$DBGREGS_BASE + $OSLAR_EL1_OFFSET}]
    ameba.dap apreg 3 4 $oslar_addr
    ameba.dap apreg 3 12 0x00000000

    set edlar_addr [expr {$DBGREGS_BASE + $EDLAR_OFFSET}]
    ameba.dap apreg 3 4 $edlar_addr
    ameba.dap apreg 3 12 0xC5ACCE55

    set edprcr_addr [expr {$DBGREGS_BASE + $EDPRCR_OFFSET}]
    ameba.dap apreg 3 4 $edprcr_addr
    ameba.dap apreg 3 12 0x00000001

    #set edprsr_addr [expr {$DBGREGS_BASE + $EDPRSR_OFFSET}]
    #set edprsr_val [ameba.dap apreg $AP_NUM $edprsr_addr]

    set edscr_addr [expr {$DBGREGS_BASE + $EDSCR_OFFSET}]
    ameba.dap apreg 3 4 $edscr_addr
    ameba.dap apreg 3 12
    set edscr_val [ameba.dap dpreg 12]
    set edscr_val [expr {$edscr_val | (1 << 14)}]
    ameba.dap apreg 3 4 $edscr_addr
    ameba.dap apreg 3 12 $edscr_addr

    echo "=================================================="
    echo "Executing CA32 debug init Done for target: $target_name"
    echo "=================================================="
}

proc ameba_ca32_reset_dummy { target_name } {
    echo "=================================================="
    echo "Executing CA32 warm reset for target: $target_name"
    echo "=================================================="
}

# Initialize global arrays to store failure counts and timestamps per core
array set _ameba_fail_counts {}
array set _ameba_first_fail_times {}

proc ameba_examine_hook { core_name } {
    global _ameba_fail_counts
    global _ameba_first_fail_times

    # Get current system time in seconds
    set current_time [clock seconds]

    # 1. Initialization Check
    # Ensure the array entry exists for this core before accessing it
    if { ![info exists _ameba_fail_counts($core_name)] } {
        set _ameba_fail_counts($core_name) 0
        set _ameba_first_fail_times($core_name) 0
    }

    # 2. Logic Evaluation
    # Retrieve the timestamp of the first failure in the current cycle
    set last_start_time $_ameba_first_fail_times($core_name)

    # Condition A:
    # 1. Previous count is 0 (First failure).
    # 2. OR Time elapsed since the first failure > 30s (Timeout reset).
    if { $_ameba_fail_counts($core_name) == 0 || ($current_time - $last_start_time) > 30 } {
        # Reset count to 1 and update the start timestamp for this core
        set _ameba_fail_counts($core_name) 1
        set _ameba_first_fail_times($core_name) $current_time
        echo "Warning: [string toupper $core_name] examine failed! Start counting (1/3 within 30s)"
    } else {
        # Condition B: Within the 30s window, increment the failure count
        incr _ameba_fail_counts($core_name)

        # Calculate time elapsed for logging purposes
        set time_elapsed [expr {$current_time - $last_start_time}]

        # Get the current count
        set current_count $_ameba_fail_counts($core_name)

        echo "Warning: [string toupper $core_name] examine failed! (Attempt $current_count/3, ${time_elapsed}s elapsed)"
    }

    # 3. Shutdown Trigger
    if { $_ameba_fail_counts($core_name) >= 3 } {
        echo "Error: [string toupper $core_name] examine failed 3 times within 30 seconds! Shutting down OpenOCD..."
        set _ameba_fail_counts($core_name) 0
        shutdown
    }
}

# -----------------------------------------------------------------
# 3. DAP and Targets
# -----------------------------------------------------------------
set _CHIPNAME ameba

source [find target/swj-dp.tcl]

swj_newdap $_CHIPNAME cpu -irlen 4 -ircapture 0x01 -irmask 0x0f
dap create $_CHIPNAME.dap -chain-position $_CHIPNAME.cpu

# --- Define the CA32 (Cortex-A32) Core ---
if { $ENABLE_CA32 } {
    set _TARGETNAME $_CHIPNAME.ca32.core
    set _CTINAME $_CHIPNAME.ca32.cti
    set DBGBASE {0x80030000 0x80032000}
    set CTIBASE {0x80038000 0x80039000}

    set _smp_command ""
    for { set _core 0 } { $_core < $_cores } { incr _core } {
        cti create $_CTINAME.$_core -dap $_CHIPNAME.dap -ap-num 3 -baseaddr [lindex $CTIBASE $_core]

        set _target_name_core "$_TARGETNAME.$_core"
        set _command "target create ${_target_name_core} aarch64 -dap $_CHIPNAME.dap \
            -coreid $_core -dbgbase [lindex $DBGBASE $_core] -cti $_CTINAME.$_core -rtos hwthread"

        eval $_command

        if { $_core != 0 } {
            set _smp_command "$_smp_command $_target_name_core"
        } else {
            set _smp_command "target smp $_target_name_core"
            ${_target_name_core} configure -event gdb-attach "ameba_ca32_debug_init ${_target_name_core}"
            ${_target_name_core} configure -gdb-port 58922
            ${_target_name_core} configure -event examine-fail { ameba_examine_hook "CA32" }
        }

        ${_target_name_core} configure -event reset-assert "ameba_ca32_reset_dummy ${_target_name_core}"
    }

    # Enable SMP
    eval $_smp_command
}

# --- Define the KM4 (Cortex-M33) Core ---
if { $ENABLE_KM4 } {
    set _KM4_TARGETNAME $_CHIPNAME.km4
    target create $_KM4_TARGETNAME cortex_m -dap $_CHIPNAME.dap -ap-num 1
    if { $GDB_PORT_SPECIFIED == 0 } {
        $_KM4_TARGETNAME configure -gdb-port 58920
    }
    $_KM4_TARGETNAME configure -gdb-max-connections 2
    $_KM4_TARGETNAME configure -event examine-fail { ameba_examine_hook "KM4" }
}

# --- Define the KM0 (Cortex-M23) Core ---
if { $ENABLE_KM0 } {
    set _KM0_TARGETNAME $_CHIPNAME.km0
    target create $_KM0_TARGETNAME cortex_m -dap $_CHIPNAME.dap -ap-num 0
    $_KM0_TARGETNAME configure -gdb-port 58921
    $_KM4_TARGETNAME configure -event examine-fail { ameba_examine_hook "KM0" }
}

# -----------------------------------------------------------------
# 4. Global Settings
# -----------------------------------------------------------------
gdb breakpoint_override hard
bindto 0.0.0.0

echo "GDB Server Init Success! ($ADAPTER_TYPE @ ${ADAPTER_SPEED}kHz)"